-- Expression evaluator

import List;
import State;
import World;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (expr, expr)         |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Ignore  (expr)

-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

fun eval (c@[s, w], expr) {
  case expr of
    Assn (e1, e2) -> case eval(c, e2) of
      [c1, value] -> case eval(c1, e1) of
        [[s1, w1], refName] -> [[s1 <- [refName, value], w1], value]
      esac
    esac
    | Seq (e1, e2) -> case eval(c, e1) of
      [c1, _] -> eval(c1, e2)
    esac
    | Skip -> [c, Void]
    | Read (st) -> case readWorld(w) of
      [v, nworld] ->  [[s <- [st, v], nworld], Void]
    esac
    | Write (e) -> case eval(c, e) of
      [[s1, w1], res] -> [[s1, writeWorld(res, w1)], Void]
    esac
    | If (e, e1, e2) -> case eval(c, e) of
      [c1, res] -> if (res != 0) then eval(c1, e1) else eval(c1, e2) fi
    esac
    | While (e, e1) -> case eval(c, e) of
      [c1, res] -> if (res) then eval(c1, Seq(e1, While(e, e1))) else [c1, Void] fi
    esac
    | DoWhile (e1, e) -> eval(c, Seq(e1, While (e, e1)) )
    | Var (st) -> [c, s(st)]
    | Ref (st) -> [c, st]
    | Const (n) -> [c, n]
    | Binop (st, e1, e2) -> case eval(c, e1) of
      [c1, res1] -> case eval(c1, e2) of
        [c2, res2] -> [c2, evalOp(st, res1, res2)]
      esac
    esac
    | Ignore (e) -> case eval(c, e) of
      [c1, _] -> [c1, Void]
    esac
  esac
}


-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState, createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
